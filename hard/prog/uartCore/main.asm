    INCDIR "../std"
    INCLUDE "std.asm"
    INCLUDE "in_ram.asm"

UART_ADDR_LOW = $FD
UART_ADDR_HIGH = $FE
UART_DATA_LENGTH = $FC
UART_HANDLER_WORD = $FA ; & $FB


UART_PING_COMMAND = 1
UART_WRITE_COMMAND = 2
UART_READ_COMMAND = 3
UART_RUN_COMMAND = 4

IRQ_HANDLER:
    subroutine
    LDA UART_STATUS_REG
    ; skip checking
    LDA UART_DATA_REG
    JMP (UART_HANDLER_WORD)

READ_UART_COMMAND:
    subroutine
    CMP #UART_PING_COMMAND
    BNE .check_write_command
    WRITE_WORD READ_PING_DATA, UART_HANDLER_WORD
    RTI
.check_write_command
    CMP #UART_WRITE_COMMAND
    BNE .check_read_command
    WRITE_WORD READ_WRITE_OFFSET_LOW, UART_HANDLER_WORD
    RTI
.check_read_command
    CMP #UART_READ_COMMAND
    BNE .ignore
    WRITE_WORD READ_READ_OFFSET_LOW, UART_HANDLER_WORD
.ignore
    RTI

READ_PING_DATA:
    CLC
    ADC #1
    STA UART_DATA_REG
    WRITE_WORD READ_UART_COMMAND, UART_HANDLER_WORD
    RTI

READ_WRITE_OFFSET_LOW:
    STA UART_ADDR_LOW
    WRITE_WORD READ_WRITE_OFFSET_HIGH, UART_HANDLER_WORD
    RTI

READ_WRITE_OFFSET_HIGH:
    STA UART_ADDR_HIGH
    WRITE_WORD READ_WRITE_LENGH, UART_HANDLER_WORD
    RTI

READ_WRITE_LENGH:
    STA UART_DATA_LENGTH
    WRITE_WORD READ_WRITE_DATA, UART_HANDLER_WORD
    LDY #0
    RTI

READ_WRITE_DATA:
    subroutine
    STA (UART_ADDR_LOW),Y
    INY
    CPY UART_DATA_LENGTH
    BNE .end
    WRITE_WORD READ_UART_COMMAND, UART_HANDLER_WORD
    LDA #$FF
    STA UART_DATA_REG
.end
    RTI

READ_READ_OFFSET_LOW:
    STA UART_ADDR_LOW
    WRITE_WORD READ_READ_OFFSET_HIGH, UART_HANDLER_WORD
    RTI

READ_READ_OFFSET_HIGH:
    STA UART_ADDR_HIGH
    WRITE_WORD READ_READ_LENGH, UART_HANDLER_WORD
    RTI

READ_READ_LENGH:
    STA UART_DATA_LENGTH
    WRITE_WORD SENDING_DATA, UART_HANDLER_WORD
    ; Enabling interrupts when transmitter is ready
    LDA #UART_COMMAND_PARITY_MOD_ENABLED | UART_COMMAND_RECEIVER_EVEN_PARITY_CHECKED | UART_COMMAND_TRANSMIT_INTERRUPT_ENABLED | UART_COMMAND_DATA_TERMINAL_READY
    STA UART_COMMAND_REG
    LDY #0
    RTI

SENDING_DATA:
    subroutine
    LDA (UART_ADDR_LOW),Y
    STA UART_DATA_REG
    INY
    CPY UART_DATA_LENGTH
    BNE .end
    WRITE_WORD SENDING_DATA_CRC, UART_HANDLER_WORD
.end
    RTI

SENDING_DATA_CRC:
    LDA #$FF
    STA UART_DATA_REG
    WRITE_WORD READ_UART_COMMAND, UART_HANDLER_WORD
    ; Disabling interrupts when transmitter is ready
    LDA #UART_COMMAND_PARITY_MOD_ENABLED | UART_COMMAND_RECEIVER_EVEN_PARITY_CHECKED | UART_COMMAND_TRANSMIT_INTERRUPT_DISABLED | UART_COMMAND_DATA_TERMINAL_READY
    STA UART_COMMAND_REG
    RTI

debug_start:
reset_start:
    ; Set control register
    LDA #UART_CONTROL_DEFAULT
    STA UART_CONTROL_REG

    ; Set command register
    LDA #UART_COMMAND_PARITY_MOD_ENABLED | UART_COMMAND_RECEIVER_EVEN_PARITY_CHECKED | UART_COMMAND_TRANSMIT_INTERRUPT_DISABLED | UART_COMMAND_DATA_TERMINAL_READY
    STA UART_COMMAND_REG

    ; Init UART handler
    WRITE_WORD READ_UART_COMMAND, UART_HANDLER_WORD

    CLI

loop:
    jmp loop
