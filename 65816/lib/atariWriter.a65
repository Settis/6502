; The module writes atari binary format from the program body to appropriate place.
.include "../include/std.inc"
.include "../include/delayImport.inc"

.import ATARI_DATA_START
.import ATARI_DATA_END

.zeropage
SRC_POINTER: 
    .res 2
DST_POINTER: 
    .res 2
DST_END_POINTER: 
    .res 2
LAST_ROM_PAGE: 
    .res 2

PAGE_ADDR_MASK = $FFC0

.code
.export COPY_ATARI_DATA
.proc COPY_ATARI_DATA
    PHP
    IND8
    A16
    STZ LAST_ROM_PAGE
    LDA #ATARI_DATA_START
    STA SRC_POINTER
@LOOP:
        LDA (SRC_POINTER)
        CMP #$FFFF
        BNE @NO_HEADER
        INC SRC_POINTER
        INC SRC_POINTER
        LDA (SRC_POINTER)
    @NO_HEADER:
        STA DST_POINTER
        INC SRC_POINTER
        INC SRC_POINTER
        LDA (SRC_POINTER)
        STA DST_END_POINTER

        ; to point to copying data
        INC SRC_POINTER
        INC SRC_POINTER

        ; copy
    @COPY_LOOP:
        LDA DST_POINTER
        AND #PAGE_ADDR_MASK
        CMP LAST_ROM_PAGE
        BEQ @SAME_PAGE
        STA LAST_ROM_PAGE
        JSR delay_10_ms
    @SAME_PAGE:
        A8
        LDA (SRC_POINTER)
        STA (DST_POINTER)
        A16
        INC SRC_POINTER
        LDA DST_POINTER
        CMP DST_END_POINTER
        BEQ @END_COPY
        INC DST_POINTER
        BRA @COPY_LOOP
    @END_COPY:

        LDA SRC_POINTER
        CMP #ATARI_DATA_END
        BCC @LOOP
    
    JSR delay_10_ms
    PLP
    RTS
.endproc

.proc delay_10_ms
.I8
    ; 62500 ticks
    LDX #226
    LDY #55
    JMP DELAY_LOOP
.endproc
